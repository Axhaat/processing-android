import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

plugins {
    id 'aar'
    id 'maven-publish'
}

dependencies {
    compileOnly name: "android"

    compileOnly "org.p5android:processing-core:${modeVersion}"

    implementationAar "com.google.vr:sdk-audio:${gvrVersion}"
    implementationAar "com.google.vr:sdk-base:${gvrVersion}" 
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        vrPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            pom {
                groupId = "org.p5android"
                artifactId = "processing-vr"
                version = "${vrLibVersion}"
                packaging = "jar"
                licenses {
                    license {
                        name = "GNU Lesser General Public License, version 2.1"
                        url = "https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt"
                        distribution = "repo"
                    }
                }
            }

            pom.withXml {
                // inserting the dependencies node
                def dependenciesNode = asNode().appendNode('dependencies')
                // start adding dependency nodes inside dependencies node
                def processingCoreDependencyNode = dependenciesNode.appendNode('dependency')
                processingCoreDependencyNode.appendNode('groupId', 'org.p5android')
                processingCoreDependencyNode.appendNode('artifactId', 'processing-core')
                processingCoreDependencyNode.appendNode('version', "${modeVersion}")
                processingCoreDependencyNode.appendNode('scope', 'implementation')

                def googleVRDependencyNode = dependenciesNode.appendNode('dependency')
                googleVRDependencyNode.appendNode('groupId', 'com.google.vr')
                googleVRDependencyNode.appendNode('artifactId', 'sdk-base')
                googleVRDependencyNode.appendNode('version', "${gvrVersion}")
                googleVRDependencyNode.appendNode('scope', 'implementation')

                def googleVRAudioDependencyNode = dependenciesNode.appendNode('dependency')
                googleVRAudioDependencyNode.appendNode('groupId', 'com.google.vr')
                googleVRAudioDependencyNode.appendNode('artifactId', 'sdk-audio')
                googleVRAudioDependencyNode.appendNode('version', "${gvrVersion}")
                googleVRAudioDependencyNode.appendNode('scope', 'implementation')
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src/"]
        }
    }
}


// Does not work because of Processing-specific tags in source code, such as @webref
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
//     archives javadocJar
    archives sourceJar
}

jar.doLast { task ->
    ant.checksum file: task.archivePath
}

clean.doFirst {
    delete "dist"
    delete "library/vr.jar"
}


compileJava.doFirst {
    String[] deps = ["sdk-audio.jar",
                     "sdk-base.jar",
                     "sdk-common.jar"]
    File libFolder = file("library")
    libFolder.mkdirs()
    for (String fn : deps) {
        Files.copy(file("${rootDir}/build/libs/" + fn).toPath(),
                   file("library/" + fn).toPath(), REPLACE_EXISTING);
    }
}


build.doLast {
    // If xml doesn't exist
    def pomfile = file("${buildDir}/publications/vrPublication/pom-default.xml")
    if (!pomfile.exists()) {
        println("************************************************************************************************\n" +
                "*                                                                                              *\n" +
                "*   File not found: root/mode/libraries/vr/build/publications/corePublication/pom-default.xml  *\n" +
                "*   First execute the following command to generate the file:                                  *\n" +
                "*   gradle generatePomFileForvrPublicationPublication                                          *\n" +
                "*                                                                                              *\n" +
                "************************************************************************************************"
        )
    }
    // // Copying vr jar to library folder
    File vrJar = file("library/vr.jar")
    vrJar.mkdirs();
    Files.copy(file("$buildDir/libs/vr.jar").toPath(),
               vrJar.toPath(), REPLACE_EXISTING);   

    // // Copying the files for release on JCentral
    File distFolder = file("dist");
    distFolder.mkdirs();
    Files.copy(file("$buildDir/libs/vr.jar").toPath(),
               file("dist/processing-vr-${vrLibVersion}.jar").toPath(), REPLACE_EXISTING);
    Files.copy(file("$buildDir/libs/vr-sources.jar").toPath(),
               file("dist/processing-vr-${vrLibVersion}-sources.jar").toPath(), REPLACE_EXISTING);
    Files.copy(file("$buildDir/libs/vr.jar.MD5").toPath(),
               file("dist/processing-vr-${vrLibVersion}.jar.md5").toPath(), REPLACE_EXISTING);
    Files.copy(file("$buildDir/publications/vrPublication/pom-default.xml").toPath(),
            file("dist/processing-vr-${vrLibVersion}.pom").toPath(), REPLACE_EXISTING);
}
